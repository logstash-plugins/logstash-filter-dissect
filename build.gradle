import java.nio.file.Files

import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "me.champeau.jmh" version "0.7.2"
}
repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'idea'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
//        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

group = 'org.logstash.dissect'
version "${new File("VERSION").text.trim()}"
description = """Dissect Filter, a Logstash plugin - the Java parts"""

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
  withJavadocJar()
}

compileJava.options.encoding = 'UTF-8'

configurations {
    provided
}

project.sourceSets {
    main.compileClasspath += project.configurations.provided
    main.runtimeClasspath += project.configurations.provided
    test.compileClasspath += project.configurations.provided
    test.runtimeClasspath += project.configurations.provided
}
project.javadoc.classpath += project.configurations.provided

idea {
    module {
        scopes.PROVIDED.plus += [project.configurations.provided]
    }
}

// logstashCoreGemPath and logstashCoreEventGemPath comes from the gradle.properties file
// the gradle.properties file is built by the Rake task bundle_install
// See BUILD_INSTRUCTIONS.md
dependencies {
    provided group: "org.apache.logging.log4j", name: "log4j-api", version: "2.17.1"
    provided group: "org.apache.logging.log4j", name: "log4j-core", version: "2.17.1"
    provided 'org.jruby:jruby-core:9.1.12.0'
    provided fileTree(dir: logstashCoreGemPath, include: '**/*.jar')
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.8.0'
    jmh fileTree(dir: logstashCoreGemPath, include: '**/*.jar')
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file("lib/jruby-dissect-library_jars.rb")
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
                def id = artifact.id.componentIdentifier
                if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                    w << "require_jar(\'${id.group}\', \'${id.module}\', \'${id.version}\')\n"
                }
            }
            w << "require_jar(\'${project.group}\', \'${project.name}\', \'${project.version}\')\n"
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
            def id = artifact.id.componentIdentifier
            if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                String artifactGroup = id.group
                String artifactName = id.module
                String artifactVersion = id.version
                String groupPath = artifactGroup.replaceAll('\\.', '/')
                File newJarFile = file("${vendorPathPrefix}/${groupPath}/${artifactName}/${artifactVersion}/${artifactName}-${artifactVersion}.jar")
                if (!newJarFile.parentFile.exists()) {
                    newJarFile.parentFile.mkdirs()
                }
                Files.copy(artifact.file.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
            }
        }

        // Copy the project jar into vendor/jar-dependencies
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${project.name}/${project.version}/${project.name}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
    }
}

vendor.dependsOn(jar, generateGemJarRequiresFile)
