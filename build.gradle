buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "me.champeau.gradle.jmh" version "0.3.1"
}
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

apply plugin: 'java'
apply plugin: 'idea'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
//        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

group = 'org.logstash.dissect'

project.sourceCompatibility = 1.8



task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier 'sources'
    extension 'jar'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
    extension 'jar'
}

task copyGemjar(type: Copy, dependsOn: sourcesJar) {
    from project.jar
    into project.file('lib/jars/')
}

task cleanGemjar {
    delete fileTree(project.file('lib/jars/')) {
        include '*.jar'
    }
}

// task copyShadowGemjar(type: Copy) {
//     from project.shadowJar
//     into project.file('lib/jars/')
// }

clean.dependsOn(cleanGemjar)
// build.dependsOn(shadowJar)

jar.finalizedBy(copyGemjar)
// shadowJar.finalizedBy(copyShadowGemjar)

configurations.create('sources')
configurations.create('javadoc')
configurations.archives {
    extendsFrom configurations.sources
    extendsFrom configurations.javadoc
}

artifacts {
    sources(sourcesJar) {
        // Weird Gradle quirk where type will be used for the extension, but only for sources
        type 'jar'
    }

    javadoc(javadocJar) {
        type 'javadoc'
    }
}

configurations {
    provided
}

project.sourceSets {
    main.compileClasspath += project.configurations.provided
    main.runtimeClasspath += project.configurations.provided
    test.compileClasspath += project.configurations.provided
    test.runtimeClasspath += project.configurations.provided
}
project.javadoc.classpath += project.configurations.provided

idea {
    module {
        scopes.PROVIDED.plus += [project.configurations.provided]
    }
}

dependencies {
    provided 'org.jruby:jruby-core:1.7.25'
    provided files('java_libs/logstash-core-event-java.jar')
    testCompile 'junit:junit:4.12'
    testCompile 'com.fasterxml.jackson.core:jackson-core:2.7.3'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    jmh compile(files('java_libs/logstash-core-event-java.jar'))
}

// See http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    description = 'Install Gradle wrapper'
    gradleVersion = '2.8'
}


